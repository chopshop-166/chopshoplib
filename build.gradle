plugins {
    id "java-library"
    id "pmd"
    id 'maven-publish'
    id "com.github.spotbugs" version "2.0.0"
    id "edu.wpi.first.GradleRIO" version "2019.4.1"
}

group = 'com.chopshop166'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    api wpi.deps.wpilib()
    api wpi.deps.vendor.java()
    nativeZip wpi.deps.vendor.jni(wpi.platforms.roborio)
    nativeDesktopZip wpi.deps.vendor.jni(wpi.platforms.desktop)
    implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'
    api group: 'com.github.Oblarg', name: 'command-rewrite-jitpack', version: '1.1.2'
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

ext {
    SpotBugsTask = com.github.spotbugs.SpotBugsTask
}

apply from: 'checks.gradle'

publishing {
    publications {
        chopshoplib(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
        }
    }
}
